(ql:quickload '(:cl-ppcre :fset))

(defun day18 (is-part-two)
  (let ((drops (fset:empty-seq))
        (start (cons 0 0)) (exit (cons 70 70)))
    (ppcre:do-register-groups ((#'parse-integer x y))
        ("(\\d+).(\\d+)\\n" (uiop:read-file-string #P"./18.txt"))
      (fset:push-last drops (cons x y)))
    (labels ((walk (obstacles)
               (loop :named outer
                     :with unwalkablep
                       := (lambda (position)
                            (let ((x (car position)) (y (cdr position)))
                              (or (not (<= 0 x 70)) (not (<= 0 y 70))
                                  (fset:member? position obstacles))))
                     :and queue := (fset:seq (cons 0 start))
                     :and visited := (fset:set start)
                     :for (n . (x . y)) := (fset:pop-first queue)
                     :while n
                     :do (loop :for (dx . dy) :in '(( 0 . -1) (0 . 1)
                                                    (-1 .  0) (1 . 0))
                               :for next := (cons (+ x dx) (+ y dy))
                               :when (equal next exit)
                                 :do (return-from outer (1+ n))
                               :unless (or (fset:member? next visited)
                                           (funcall unwalkablep next))
                                 :do (fset:push-last queue (cons (1+ n) next))
                                 :and :do (fset:includef visited next)))))
      (if is-part-two
          (let ((obstacles (fset:set)))
            (fset:do-seq (obstacle drops)
              (fset:includef obstacles obstacle)
              (unless (walk obstacles)
                (return (format nil "~d,~d" (car obstacle) (cdr obstacle))))))
          (walk (fset:range (fset:subseq drops 0 1024)))))))
